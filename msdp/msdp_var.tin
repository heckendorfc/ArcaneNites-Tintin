#class msdpvar kill;
#class msdpvar open;

#alias {eventstat} {
	#format shortstat {%.3s} {%1};
	#$pcs #var {cs[$shortstat][$myses]} {%2};

	updatestats;
}

#alias {clearaffects} {
	#if {&ca[]>0} {
		#foreach {$ca[]} {aff_name} {
			#$pcs #var {ca[$aff_name][%1]} {-1};
		};
	};
};

#alias {parseaffects} {
	#$pcs clearaffects $myses;
	#$pcs #var {ca[Affect][$myses]} {0};
	#foreach {$affects[]} {aff_name} {
		#$pcs #var {ca[$aff_name][$myses]} {$affects[$aff_name]};
	};
};

#alias {eventaffects} {
	#var {affects} {%2};
	parseaffects;
	updateaffects;
}

#alias {parseexits} {
	#var {exit_vnum} {};
	#var {exit_door} {};
	#foreach {$map_exits[]} {ename} {
		#var tmpexit {};
		#parse {$map_exits[$ename]} {echar} {
			#if {"$echar" >= "0" && "$echar" <= "9"} {
				#var {tmpexit} {$tmpexit$echar};
			};
			#else {
				#var {exit_door[$ename]} {$echar}
			};
		}
		#var {exit_vnum[$ename]} {$tmpexit}
	};
}

#alias {eventother} {
	#var {c_hp} {$MSDP_HEALTH};
	#var {c_hpm} {$MSDP_HEALTH_MAX};
	#math {c_hpp} {$MSDP_HEALTH*100/$MSDP_HEALTH_MAX};

	#var {c_mp} {$MSDP_MANA};
	#var {c_mpm} {$MSDP_MANA_MAX};
	#math {c_mpp} {$MSDP_MANA*100/$MSDP_MANA_MAX};

	#var {c_mv} {$MSDP_MOVEMENT};
	#var {c_mvm} {$MSDP_MOVEMENT_MAX};
	#math {c_mvp} {$MSDP_MOVEMENT*100/$MSDP_MOVEMENT_MAX};

	#var {c_align} {$MSDP_ALIGNMENT};

	#var {op_n} {$MSDP_OPPONENT_NAME};
	#var {op_lvl} {$MSDP_OPPONENT_LEVEL};
	#var {op_hp} {$MSDP_OPPONENT_HEALTH};


	#$pcs #var gstat[$myname] {${myname}: ${c_hpp}% hp  |  ${c_mpp}% mp  |  ${c_mvp}% mv  |  Align: $c_align};

	updateprompt;
}

#alias {eventmap} {
	#var {map_exits} {$MSDP_ROOM_EXITS};
	#var {map_name} {$MSDP_ROOM_NAME};
	#var {map_area} {$MSDP_AREA_NAME};

	parseexits;
	
	#var {exits} {};
	#foreach {n;north;e;east;s;south;w;west;u;up;d;down} {exit}
	{
		#if {&MSDP_ROOM_EXITS[$exit] == 0}
		{
			#continue
		};

		#if {"$MSDP_ROOM_EXITS[$exit]" == "{O|OPEN}"}
		{
			#var {exits} {$exits<138>$DIR_NAME[$exit]}
		};
		#else
		{
			#var {exits} {$exits<168>$DIR_NAME[$exit]}
		}
	};
};

#alias {eventmapfinish} {
	#var {map_vnum} {$MSDP_ROOM_VNUM};
	updatemap;
};

#event {IAC SB MSDP} {
	#var {MSDP_%0} {%1};

	#if {"%0" == "{HITROLL|DAMROLL|SAVES|AC|INT|WIS|STR|DEX|CON|CHA}"} {
		eventstat {%0} {%1};
	};
	#elseif {"%0" == "{AFFECTS}"} {
		eventaffects {%0} {%1};
	};
	#elseif {"%0" == "{HEALTH|HEALTH_MAX|MANA|MANA_MAX|MOVEMENT|MOVEMENT_MAX|ALIGNMENT|OPPONENT_NAME|OPPONENT_LEVEL|OPPONENT_HEALTH}"} {
		eventother;
	};
	#elseif {"%0" == "{ROOM_EXITS|ROOM_NAME|AREA_NAME}"} {
		eventmap;
	};
	#elseif {"%0" == "ROOM_VNUM"} {
		eventmapfinish;
	};
	#else {
		#nop {#showme {Unhandled MSDP %0 = %1};};
	};
};

#class msdpvar close;
