#class msdp kill;
#class msdp open;

#format IAC  %a 255
#format DONT %a 254
#format DO   %a 253
#format WONT %a 252
#format WILL %a 251
#format SB   %a 250
#format SE   %a 240

#format MSDP %a  69

#format VAR  %a  01
#format VAL  %a  02

#var REPORTABLE_VARIABLES {
{HEALTH};
{HEALTH_MAX};
{MANA};
{MANA_MAX};
{MOVEMENT};
{MOVEMENT_MAX};
{EXPERIENCE_TNL};
{EXPERIENCE_TNL_MAX};
{MONEY};
{ROOM_EXITS};
{ROOM_NAME};
{ROOM_VNUM};
{AREA_NAME};
{AFFECTS};
{ALIGNMENT};
{OPPONENT_HEALTH};
{OPPONENT_LEVEL};
{OPPONENT_NAME};
{HITROLL};
{DAMROLL};
{SAVES};
{AC};
{STR};
{INT};
{WIS};
{CON};
{DEX};
{CHA};
}

#nop Turn telnet debug off once everything works as it's spammy.

#config {debug telnet} {off}

#event {IAC WILL MSDP}
{
        #send {$IAC$DO$MSDP\};
}
/*
#event {IAC SB MSDP}
{
        #var {MSDP_%0} {%1};
}
*/
#nop Ask the server to report all keys inside REPORTABLE_VARIABLES
#alias {msdp_report}
{
        #var result {$IAC$SB$MSDP${VAR}REPORT};

        #foreach {$REPORTABLE_VARIABLES[]} {tmp}
        {
                #var result {$result${VAL}$tmp}
        };

        #send {$result$IAC$SE\}
}

#alias {msdp_list}
{
        #send {$IAC$SB$MSDP${VAR}LIST${VAL}%0$IAC$SE\}
}

#alias {msdp_send}
{
        #send {$IAC$SB$MSDP${VAR}SEND${VAL}%0$IAC$SE\}
}

#class msdp close;
